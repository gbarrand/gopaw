mess **************************************************
mess '==> Example on how to manipulate 1dim arrays'
mess '==> We are processing TDC values (ns) corresponding'
mess '==> to a test-beam profile as reconstructed by'
mess '==> a Drift Chamber'
mess **************************************************
set *
opt *
opt ZFL
SLEEP_TIME = 1
render
wait ! [SLEEP_TIME]

mess **********************************************************
mess  '==> Reading from an input file vectors: help VECTOR/READ'
mess **********************************************************
VECTOR/READ pTDC1P,mTDC1P,dTDC1P,pTDC1M,mTDC1M,dTDC1M _
Channel1.dat 'F7.1,1X,F7.1,1X,F7.1,1X,F7.1,1X,F7.1,1X,F7.1' 'OC'
render
wait ! [SLEEP_TIME]

mess **************************************************************
mess  '==> Example of printing a filled 1dim array: help VECTOR/PLOT'
mess **************************************************************
VECTOR/PRINT mTDC1P(1:10)
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Dumping to output file the vectors: help VECTOR/WRITE'
mess ***********************************************************
VECTOR/WRITE pTDC1P,mTDC1P,dTDC1P,pTDC1M,mTDC1M,dTDC1M _
out.dat 'F7.1,1X,F7.1,1X,F7.1,1X,F7.1,1X,F7.1,1X,F7.1' 'OC'
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Filling one pre-declared 1dim histo with one array'
mess  '==>  help HISTOGRAM/CREATE/1DHISTO; help VECTOR/HFILL' 
mess ***********************************************************
1dhisto 10 '' 100 500 1000
VECTOR/HFILL PTDC1P 10
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Plotting the filled histo; help hi/plot' 
mess  '==> WITHOUT statistical error bar plotted'
mess ***********************************************************
hi/plot 10 
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Plotting the filled histo; help hi/plot' 
mess  '==> WITH statistical error bar plotted'
mess ***********************************************************
SET HWID 5
hi/plot 10 e
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> NOW SOME COSMETICS ...'
mess  '==> increase the histo lines; SET HWID 10'
mess ***********************************************************
SET HWID 10
hi/plot 10 e
render
wait ! [SLEEP_TIME]

mess  'What if we now try again with stat bars off ? '
hi/plot 10 
render
wait ! [SLEEP_TIME]

mess  'What if we now try again with stat bars on but in blue ? '
SET HCOL 4
hi/plot 10 e
render
wait ! [SLEEP_TIME]

hi/plot 10 
ATITLE '[h]' N
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Change the font for the axis labels; e.g. SET VFON <20>'
mess ***********************************************************
SET VFON 20
hi/plot 10
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> We need the statistical info on the histo: SET STAT'
mess ***********************************************************
opt STAT
hi/plot 10 e
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Let us perform a gaussian fit: help HISTO/FIT'
mess ***********************************************************
hi/fit 10 g
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Display the fit result in the stat box: OPT FIT'
mess ***********************************************************
OPT FIT
hi/plot 10 e
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Change to red color and the width of fit function:'
mess  'SET FCOL 2; SET FWID 10'
mess ***********************************************************
SET FCOL 2 
SET FWID 10
hi/plot 10 e
render
wait ! [SLEEP_TIME]

mess ****************************************************************
mess  '==> Set title and change its font: help ATITLE; SET LFON <20>'
mess  'WARNING: Poor quality of the fonts'
mess ****************************************************************
SET LFON 20
hi/plot 10 e
ATITLE 'Drift Chamber TDC (ns)' 'Yield'
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Trying to displace the statistical box ...'
mess  '==> Margin Right/Up: SET SMGR XX; SET SMGU YY;'
mess ***********************************************************
SET SMGR 0.4
SET SMGU 0.1
hi/plot 10 e
ATITLE 'Drift Chamber TDC (ns)' 'Yield'
render
wait ! [SLEEP_TIME]

OPT BACK
set CFON 62
hi/plot 10 e
ATITLE 'Drift Chamber TDC (ns)' 'Yield'
render
wait ! [SLEEP_TIME]

PICTURE/PRINT back.eps
KUIP/PSVIEW back.eps
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> BY THE WAY, the histo is not completely contained in the'
mess  'plot frame. Some margin up should be given by default!'
mess  'Let us try with the NULL command: e.g. NULL 500 1000 0 40'
mess ***********************************************************
OPT NBAC
set CFON 0
NULL 500 1000 0 50
hi/plot 10 e
ATITLE 'Drift Chamber TDC (ns)' 'Yield'
render
wait ! [SLEEP_TIME]

mess ***********************************************************
mess  '==> Saving the plot in file rfabbri_vec_2.eps'
mess  'help PICTURE/PRINT'
mess ***********************************************************
PICTURE/PRINT rfabbri_vec_2.eps
KUIP/PSVIEW rfabbri_vec_2.eps
