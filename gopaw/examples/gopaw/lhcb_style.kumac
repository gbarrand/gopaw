macro lhcb_style

* Example kumac for demonstrating the use of lhcbstyle.kumac.
* Simple Gauss distribution with fit.
   
   exec lhcb_def.kumac
   
* optionally, switch on stat and fit boxes:
* opt stat; set stat 1110
* opt fit;  set fit 1011

   1d 10 'invariant mass' 40 5200. 5400.
 
   mean = 5280.
   sigma = 17.

   nev = 1000
   do j=1,[nev]
     n = 100
     *vec/del a
     sigma a = array([n],-1#1)
     sigma b = 2.*rndm(a)-1.
     sigma c = vsum(b)
     s = $sigma(vsum(b))
     scale = $sigma(sqrt(3.0/[n]))
     s = [mean]+[sigma]*[scale]*[s]
     call hfill(10,[s],0.,1.)
   enddo

   hist/fit 10 'g'
   set htyp 354
   hist/set/max 10 140.

*/////////////////////////////////////////////////////////////////////
*/// G.Barrand : for fonts, 2 = hershey.
*///  If using freetype, the PAW encoding in the below atit and itx
*///  do not work (yet) with gopaw. With CERN-PAW, on the screen
*///  the PAW encoding is not evaluated, but it is ok in the eps
*///  production which is ok since lhcb_def.kumac is intended for
*///  publication printing. Here we use hershey and use the TWID
*///  attribute (gopaw specific) to change the text line width.
*///  It looks ok both on screen and with INZB_PNG.
*/////////////////////////////////////////////////////////////////////
   set LFON 2
   set CFON 2
   set TWID 7
*/////////////////////////////////////////////////////////////////////
   
   hist/plot 10
*** for error bars :
*   hist/plot 10 e
*** without fit curve :
*   hist/plot 10 hist

   atit 'm(K^+![p]^-!) "M#GeV/c^2!"N#' 'Entries'
  *atit 'm(K+pi-) [GeV/c2]' 'Entries'	   
   tick

   set chhe 1.1
   itx 5330. 118. 'LHCb'    
   set chhe 0.5
   itx 5325.  90. 'final selection'

   set chhe 0.7
   itx 5325. 102. 'B?d! "5# K^+![p]^-!'
  *itx 5335. 100. 'Bd -> K+ pi-'

   pic/print testLhcbStylePAW.eps

return
