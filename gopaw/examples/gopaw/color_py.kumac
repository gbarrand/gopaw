MACRO color
**************************************************************************
* Color example.                                                         *
**************************************************************************
exec alldef
zon 4 4
fun/fun2 10 cone.py 10 -12 12 12 -12 12 ' '
hi/plot 10 TEXT
* Plot wired contour (OPT=0) with some internal palette
* (PALNB=51 in gopaw (black,red,green,blue)).
* 20 levels are required. Colors is going from data min to max.
* (SoPlotter : repContour_xy)
opt linz
hi/2d/con 10 20 0
opt logz
hi/2d/con 10 20 0
* Plot filled contour (OPT=3) with the default palette 1
* (red,green,blue,yellow,magenta,cyan).
* Set HCOL 0.<PALNB> permits to select the palette.
* 20 levels are required. Colors is looping within 
* the selected palette : 
* (SoPlotter : repContour_xy_filled)
set hcol 0.1
opt linz
hi/2d/con 10 20 3
opt logz
hi/2d/con 10 20 3
* Plot the histogram (and not function) with some 
* default palette (0 in gopaw)
* (white,yellow,cyan,green,magenta,red,blue,black)) :
* Colors is going from data min to max.
* (SoPlotter : repBins2D_solid_one)
hi/plot 10 colz
opt linz
hi/2d/con 10 20 3
hi/2d/con 10 20 0
* Create the palette 2. With the below command it contains
* color elements (0,5,7,3,6,2,4,1) :
set ncol 8
palette 2
* Change the palette 2 to swap yellow and green ;
* then redefine first the color elements :
color_table 5 0 1 0
color_table 3 1 1 0
* then redefine the palette 2 :
palette 2
* Plot boxes with the palette 2 :
set hcol 0.2
hi/plot 10 colz
* Draw a 3D surface according to palette 2 :
* (SoPlotter : repTopFaces2D_xyz)
surf 10 ! ! 1
opt logz
surf 10 ! ! 1
opt linz
* Create a grey scale palette :
set ncol 16
palette 3 8 8 9 10 11 12 13 14 15
* Plot filled contour with the new palette 3 :
set hcol 0.3
hi/2d/con 10 20 3
* Create a blue to red palette :
palette 4
* Plot filled contour with the new palette 3 :
set hcol 0.4
hi/2d/con 10 20 3
hi/del 10
RETURN

